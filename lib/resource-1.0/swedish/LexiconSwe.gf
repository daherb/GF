--# -path=.:../scandinavian:../common:../abstract:../../prelude

concrete LexiconSwe of Lexicon = CatSwe ** open ParadigmsSwe, IrregSwe in {

flags 
  optimize=values ;

lin
  airplane_N = regGenN "flygplan" neutrum ;
  answer_V2S = mkV2S (regV "svarar") "till" ;
  apartment_N = mk2N "lägenhet" "lägenheter" ;
  apple_N = regGenN "äpple" neutrum ;
  art_N = mk2N "konst" "konster" ;
  ask_V2Q = mkV2Q (regV "frågar") [] ;
  baby_N = regGenN "bebis" utrum ;
  bad_A = irregA "dålig" "sämre" "sämst";
  bank_N = mk2N "bank" "banker" ;
  beautiful_A = mk3A "vacker" "vackert" "vackrast" ;
  become_VA = mkVA (mkV "bli" "blir""bli" "blev" "blivit" "bliven") ;
  beer_N = regGenN "öl" neutrum ;
  beg_V2V = mkV2V (mkV "be" "ber""be" "blad" "bett" "bedd") [] "att" ;
  big_A = irregA "stor" "större" "störst";
  bike_N = mk2N "cykel" "cyklar" ;
  bird_N = mk2N "fågel" "fåglar" ;
  black_A = mk2A "svart" "svart" ;
  blue_A = mk2A "blå" "blått";
  boat_N = regGenN "båt" utrum ;
  book_N = mkN "bok" "boken" "böcker" "böckerna" ;
  boot_N = mk2N "stövel" "stövlar" ;
  boss_N = mk2N "chef" "chefer" ;
  boy_N = regGenN "pojke" utrum ;
  bread_N = regGenN "bröd" neutrum ;
  break_V2 = dirV2 (partV (mkV "slå" "slår" "slå" "slog" "slagit" "slagen") "sönder") ;
  broad_A = mk2A "bred" "brett" ;
  brother_N2 = mkN2 ((mkN "bror" "brodern" "bröder" "bröderna")) "till" ;
  brown_A = regA "brun" ;
  butter_N = regGenN "smör" neutrum ;
  buy_V2 = dirV2 (mk2V "köpa" "köpte") ;
  camera_N = regGenN "kamera" utrum ;
  cap_N = regGenN "mössa" utrum ;
  car_N = regGenN "bil" utrum ;
  carpet_N = regGenN "matta" utrum ;
  cat_N = mk2N "katt" "katter" ;
  ceiling_N = regGenN "tak" neutrum ;
  chair_N = regGenN "stol" utrum ;
  cheese_N = regGenN "ost" utrum ;
  child_N = regGenN "barn" neutrum ;
  church_N = regGenN "kyrka" utrum ;
  city_N = mkN "stad" "staden" "städer" "städerna" ;
  clean_A = regA "ren" ;
  clever_A = regA "klok" ;
  close_V2 = dirV2 (mk2V "stänga" "stängde") ;
  coat_N = regGenN "rock" utrum ;
  cold_A = regA "kall" ;
  come_V = komma_V ;
  computer_N = mk2N "dator" "datorer" ;
  country_N = mkN "land" "landet" "länder" "länderna" ;
  cousin_N = mk2N "kusin" "kusiner" ;
  cow_N = mk2N "ko" "kor" ;
  die_V = (mkV "dö" "dör" "dö" "dog" "dött" "dödd") ; ----
  distance_N3 = mkN3 (mk2N "avstånd" "avstånd") "från" "till" ;
  dirty_A = regA "smutsig" ;
  doctor_N = mk2N "läkare" "läkare" ;
  dog_N = regGenN "hund" utrum ;
  door_N = regGenN "dörr" utrum ;
  drink_V2 = dirV2 (irregV "dricka" "drack" "druckit") ;
  easy_A2V = mkA2V (mk2A "lätt" "lätt") "för" ;
  eat_V2 = dirV2 (irregV "äta" "åt" "ätit") ;
  empty_A = mkA "tom" "tomt" "tomma" "tomma" "tommare" "tommast" "tommaste" ;
  enemy_N = regGenN "fiende" neutrum ;
  factory_N = mk2N "fabrik" "fabriker" ;
  father_N2 = mkN2 ((mkN "far" "fadern" "fäder" "fäderna")) "till" ;
  fear_VS = mkVS (regV "fruktar") ;
  find_V2 = dirV2 (irregV "finna" "fann" "funnit") ;
  fish_N = mk2N "fisk" "fiskar" ;
  floor_N = regGenN "golv" neutrum ;
  forget_V2 = dirV2 (mkV "glömma" "glömmer" "glöm" "glömde" "glömt" "glömd") ;
  fridge_N = regGenN "kylskåp" neutrum ;
  friend_N = mkN "vän" "vännen" "vänner" "vännerna" ;
  fruit_N = mk2N "frukt" "frukter" ;
  fun_AV = mkAV (regA "rolig") ;
  garden_N = regGenN "trädgård" utrum ;
  girl_N = regGenN "flicka" utrum ;
  glove_N = regGenN "handske" utrum ;
  gold_N = regGenN "guld" neutrum ;
  good_A = mkA "god" "gott" "goda" "goda" "bättre" "bäst" "bästa" ;
  go_V = (mkV "gå" "går" "gå" "gick" "gått" "gången") ;
  green_A = regA "grön" ;
  harbour_N = regGenN "hamn" utrum;
  hate_V2 = dirV2 (regV "hatar") ;
  hat_N = regGenN "hatt" utrum ;
  have_V2 = dirV2 (mkV "ha" "har" "ha" "hade" "haft" "haft") ; ---- pp
  hear_V2 = dirV2 (mkV "höra" "hör" "hör" "hörde" "hört" "hörd") ;
  hill_N = regGenN "kulle" utrum ;
--  hope_VS = mkVS ((regV "önskar")) ;
  hope_VS = mkVS (depV (regV "hoppar")) ;
  horse_N = regGenN "häst" utrum ;
  hot_A = regA "het" ;
  house_N = regGenN "hus" neutrum ;
  important_A = regA "viktig" ;
  industry_N = mk2N "industri" "industrier" ; ---- "ien" ??
  iron_N = regGenN "järn" neutrum ;
  king_N = (regGenN "kung" utrum) ;
  know_V2 = dirV2 (mkV "veta" "vet" "vet" "visste" "vetat" "visst") ;
  lake_N = mkN "sjö" "sjön" "sjöar" "sjöarna" ;
  lamp_N = regGenN "lampa" utrum;
  learn_V2 = dirV2 (reflV (mkV "lära" "lär" "lär" "lärde" "lärt" "lärd")) ;
  leather_N = mkN "läder" "lädret" "läder" "lädren" ;
  leave_V2 = dirV2 (regV "lämnar") ;
  like_V2 = mkV2 (mk2V "tycka" "tyckte") "om" ;
  listen_V2 = mkV2 (regV "lyssnar") "på" ;
  live_V = (irregV "leva" "levde" "levt") ; ---- ?
  long_A = irregA "lång" "längre" "längst" ;
  lose_V2 = dirV2 (regV "förlora") ;
  love_N = regGenN "kärlek" utrum ;
  love_V2 = dirV2 (regV "älska") ;
  man_N = (mkN "man" "mannen" "män" "männen") ;
  married_A2 = mkA2 (mk2A "gift" "gift") "med" ;
  meat_N = regGenN "kött" neutrum ;
  milk_N = regGenN "mjölk" utrum ; ---- -ar?
  moon_N = regGenN "måne" utrum ;
  mother_N2 = mkN2 (mkN "mor" "modern" "mödrar" "mödrarna") "till" ;
  mountain_N = regGenN "berg" neutrum ;
  music_N = mk2N "musik" "musiker" ; ---- er ?
  narrow_A = regA "smal" ;
  new_A = mkA "ny" "nytt" "nya" "nya" "nyare" "nyast" "nyaste" ;
  newspaper_N = regGenN "tidning" utrum ;
  oil_N = regGenN "olja" utrum ;
  old_A = mkA "gammal" "gammalt" "gamla" "gamla" "äldre" "äldst" "äldsta" ;
  open_V2 = dirV2 (regV "öppna") ;
  paint_V2A = mkV2A (regV "måla") [] ;
  paper_N = mkN "papper" "pappret" "papper" "pappren" ;
  paris_PN = regPN "Paris" neutrum ;
  peace_N = regGenN "fred" utrum ; ---- ar?
  pen_N = regGenN "penna" utrum ;
  planet_N = mk2N "planet" "planeter" ;
  plastic_N = mk2N "plast" "plaster" ;
  play_V2 = dirV2 (regV "spela") ;
  policeman_N = mk2N "polis" "poliser" ;
  priest_N = mk2N "präst" "präster" ;
  probable_AS = mkAS (regA "sannolik") ;
  queen_N = regGenN "drottning" utrum ;
  radio_N = mkN "radio" "radion" "radioapparater" "radioapparaterna" ; -- SAOL
  rain_V0 = mkV0 (regV "regna") ;
  read_V2 = dirV2 (mk2V "läsa" "läste") ;
  red_A = mk2A "röd" "rött" ;
  religion_N = mk2N "religion" "religioner" ;
  restaurant_N = mk2N "restaurang" "restauranger" ;
  river_N = mkN "å" "ån" "åar" "åarna" ;
  rock_N = mkN "sten" "stenen" "stenar" "stenarna" ;
  roof_N = regGenN "tak" neutrum ;
  rubber_N = mkN "gummi" "gummit" "gummin" "gummina" ;
  run_V = (irregV "springa" "sprang" "sprungit") ;
  say_VS = mkVS (mkV "säga" "säger" "säg" "sade" "sagt" "sagd") ;
  school_N = regGenN "skola" utrum;
  science_N = mk2N "vetenskap" "vetenskaper" ;
  sea_N = mkN "sjö" "sjön" "sjöar" "sjöarna" ;
  seek_V2 = dirV2 (mk2V "söka" "sökte") ;
  see_V2 = dirV2 (mkV "se" "ser" "se" "såg" "sett" "sedd") ;
  sell_V3 = dirV3 (irregV "sälja" "sålde" "sålt") "till" ;
  send_V3 = dirV3 (regV "skicka") "till" ;
  sheep_N = mk2N "får" "får" ;
  ship_N = regGenN "skepp" neutrum ;
  shirt_N = regGenN "skjorta" utrum ;
  shoe_N = mk2N "sko" "skor" ;
  shop_N = mk2N "affär" "affären" ;
  short_A = regA "kort" ;
  silver_N = mkN "silver" "silvret" "silver" "silvren" ;
  sister_N = mk2N "syster" "systrar" ;
  sleep_V = (irregV "sova" "sov" "sovit") ;
  small_A = mkA "liten" "litet" "lilla" "små" "mindre" "minst" "minsta" ;
  snake_N = regGenN "orm" utrum ;
  sock_N = regGenN "strumpa" utrum ;
  speak_V2 = dirV2 (regV "tala") ;
  star_N = regGenN "stjärna" utrum ;
  steel_N = regGenN "stål" utrum ;
  stone_N = mkN "sten" "stenen" "stenar" "stenarna" ;
  stove_N = regGenN "spis" utrum ;
  student_N = mk2N "student" "studenter" ;
  stupid_A = mk3A "dum" "dumt" "dumma" ;
  sun_N = regGenN "sol" utrum ;
  switch8off_V2 = dirV2 (partV (irregV "stänga" "stängde" "stängt") "av") ;
  switch8on_V2 = dirV2 (partV (irregV "slå" "slog" "slagit") "på") ;
  table_N = regGenN "bord" neutrum ;
  talk_V3 = mkV3 (regV "prata") "till" "om" ;
  teacher_N = mk2N "lärare" "lärare" ;
  teach_V2 = dirV2 (regV "undervisa") ;
  television_N = mk2N "television" "televisioner" ;
  thick_A = regA "tjock" ;
  thin_A = mk2A "tunn" "tunt" ;
  train_N = regGenN "tåg" neutrum ;
  travel_V = mk2V "resa" "reste" ;
  tree_N = regGenN "träd" neutrum ;
  ----  trousers_N = regGenN "trousers" ; ---- pl t !
  ugly_A = regA "ful" ;
  understand_V2 = dirV2 (mkV "förstå" "förstår" "förstå" "förstod" "förstått" "förstådd") ;
  university_N = regGenN "universitet" neutrum ;
  village_N = mkN "by" "byn" "byar" "byarna" ;
  wait_V2 = mkV2 (regV "vänta") "på" ;
  walk_V = (mkV "gå" "går" "gå" "gick" "gått" "gången") ;
  warm_A = regA "varm" ;
  war_N = regGenN "krig" neutrum ;
  watch_V2 = mkV2 (regV "titta") "på" ;
  water_N = mkN "vatten" "vattnet" "vatten" "vattnen" ;
  white_A = regA "vit" ;
  window_N = mkN "fönster" "fönstret" "fönster" "fönstren" ;
  wine_N = mkN "vin" "vinet" "viner" "vinerna" ; ----
  win_V2 = dirV2 (irregV "vinna" "vann" "vunnit") ;
  woman_N = regGenN "kvinna" utrum ;
  wonder_VQ = mkVQ (regV "undra") ;
  wood_N = mkN "trä" "träet" "träen" "träena" ; ---- ?
  write_V2 = dirV2 (irregV "skriva" "skrev" "skrivit") ;
  yellow_A = regA "gul" ;
  young_A = irregA "ung" "yngre" "yngst" ;

  do_V2 = dirV2 (mkV "göra" "gör" "gör" "gjorde" "gjort" "gjord") ;
  now_Adv = mkAdv "nu" ;
  already_Adv = mkAdv "redan" ;
  song_N = mk2N "sång" "sånger" ;
  add_V3 = mkV3 (partV (irregV "lägga" "lade" "lagt") "till") [] "till" ;
  number_N = mkN "nummer" "numret" "numren" "numren" ;
  put_V2 = mkV2 (mkV "sätta" "sätter" "sätt" "satte" "satt" "satt") [] ;
  stop_V = regV "stanna" ;
  jump_V = regV "hoppa" ;

} ;
